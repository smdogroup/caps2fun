#wing.csm similar to afrl4_wing.csm by John Dannenhoffer
#written by Sean Engelstad

#naca airfoil wing with finite differencing for mesh sensitivity

#configuration parameters
cfgpmtr  nspar  1  -
cfgpmtr  nrib  1  -
cfgpmtr cfdOn 0 -

#default design parameters
despmtr  area  15  len^2
despmtr  aspect  6.0  -
despmtr  taper  0.5  -
despmtr  ctwist  5.0  deg
despmtr  lesweep  30.0 deg
despmtr  dihedral 5.0  deg
despmtr tc0 0.1
despmtr tcf 0.1
despmtr camb0 0.0
despmtr cambf 0.0

#internal parameters
set  cmean sqrt(area/aspect)
set  span cmean*aspect
set  sspan span/2
set  croot 2*cmean/(1+taper)
set  ctip croot*taper
set  xtip sspan*tand(lesweep)
set  ytip sspan*tand(dihedral)
set  margin 0.02*cmean
set  ybot -0.2*croot-margin
set  ytop 0.2*croot+ytip+margin

#make two base airfoils
udprim  naca thickness tc0 Camber camb0
scale  croot
udprim  naca thickness tcf Camber cambf
scale   ctip
rotatez		-ctwist	0		0
translate	xtip	ytip	-sspan
loft		0
attribute	OML	0

patbeg foo ifzero(cfdOn,1,0)
   #store OML of wing
   store OML
   #dump	OML.egads	1

   #spar parameters
   set 	ctipEff		ctip*cosd(ctwist)

   #spars
   patbeg		ispar		nspar
       #import OML.egads	
       set		frac		ispar/(nspar+1)
       set		dx			frac*(croot-ctipEff)-xtip
       set		hyp			sqrt(sspan*sspan+dx*dx)
       box 0 ybot margin 0 ytop-ybot -hyp-2*margin
       set		localSweep	atand(dx/sspan)
       rotatey localSweep 0 0
       translate	frac*croot	0	0
       select face
           attribute capsGroup !$spar
       attribute spar ispar

       #we leave spars un-unioned here
   patend

   #ribs
   patbeg		irib		nrib
       set		dxmax		max(croot,xtip+ctipEff)+2*margin
       set		zcoord		-(irib)*sspan/(nrib+1)
       box 0 ybot zcoord dxmax ytop-ybot 0
       select face
           attribute capsGroup !$rib

       union
       patbeg      foo  ifzero(irib-1,nspar-1,0)
           union
       patend
       #union with previous
   patend 

   #fillet 0.1 191

   #trim ribs and spars to OML
   #import OML.egads
   restore OML

   patbeg foo ifzero(cfdOn,1,0)
         intersect
      #import OML.egads
      restore OML
      hollow 0
   patend
   
patend



select face
	attribute capsGroup $OML
	attribute capsMesh $OML

patbeg foo ifzero(cfdOn,1,0)
   union
patend

patbeg foo cfdOn
   store OML

   set ff 20*cmean
   set df 2*ff
   #farfield
   box -ff -ff -ff df df df
   select face
      attribute capsGroup $Farfield
      attribute capsMesh $Farfield
   store box

   restore box
   restore OML
   subtract
patend


#capsAIM attribute
select body
	attribute capsAIM $egadsTessAIM;tacsAIM;pointwiseAIM;fun3dAIM
	attribute capsMeshLength 1.0

patbeg foo ifzero(cfdOn,1,0)

   #capsConstraint arrays
   dimension rootFaces 1 2
   cfgpmtr rootFaces "11;18"
   dimension rootEdges 1 5
   cfgpmtr rootEdges "13;18;21;26;8"
   dimension bottomFaces 1 4
   cfgpmtr bottomFaces "2;9;3;12"
   dimension rootNodes 1 4
   cfgpmtr rootNodes "6;9;7;15

   #capsConstraint and capsLoad
   patbeg find 2
      select face !rootFaces[1,find]
          attribute capsConstraint $wingRoot
   patend
   patbeg eind 5
      select edge !rootEdges[1,eind]
          attribute capsConstraint $wingRoot
   patend
   patbeg nind 4
      select node !rootNodes[1,nind]
          attribute capsConstraint $wingRoot
   patend
   patbeg find 4
      select face !bottomFaces[1,find]
          attribute capsLoad $bottomWing
   patend
patend
patbeg foo cfdOn
   dimension rootFaces 1 1
   cfgpmtr rootFaces "10"
   dimension rootEdges 1 2
   cfgpmtr rootEdges "15;20"
   dimension rootNodes 1 2
   cfgpmtr rootNodes "11;12"
   dimension bottomFaces 1 1
   cfgpmtr bottomFaces "9"
   
   #capsConstraint and capsLoad
   select face 10
   	  attribute capsConstraint $wingRoot
   select edge 15
   	  attribute capsConstraint $wingRoot
   select edge 20
      attribute capsConstraint $wingRoot
   select node 11
   	  attribute capsConstraint $wingRoot
   select node 12
      attribute capsConstraint $wingRoot
   select face 9
       attribute capsLoad $bottomWing
patend

patbeg foo cfdOn
   dump cfd.egads
patend
patbeg foo ifzero(cfdOn,1,0)
   dump wing.egads
patend
#restore wing
